apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'de.fhg.igd.iva'
version = '1.0-SNAPSHOT'

mainClassName = "main.MyMain"

// We use both Maven Central and the Humboldt Artifactory instance, which contains igd-iva/pcolor
repositories {
    mavenCentral()
    maven {
        url "http://artifactory.esdi-humboldt.eu/artifactory/libs-release/"
    }
}

dependencies {
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
	compile group: 'org.reflections', name: 'reflections', version: '0.9.8'
	compile group: 'org.antlr', name: 'ST4', version: '4.0.8'
	compile group: 'com.google.guava', name: 'guava', version: '17.0'
	
	// In Humboldt Artifactory
	compile(group: 'de.fhg.igd', name: 'pcolor', version: '1.3.0') 
	
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
	
	runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}


task sourceJar(type: Jar) {
    description = "Create a JAR with all sources"
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = "Create a JAR with the JavaDoc for the java sources"
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Define the artifacts we want to publish (the .pom will also be included since the Maven plugin is active)
artifacts {
    archives sourceJar
    archives javadocJar
}
